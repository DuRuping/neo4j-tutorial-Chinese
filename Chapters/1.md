 
neo4j 课程 (缺一只neo4j娘？)

我们已经开了上百次"Intro to Neo4j"课程，许多听众表示这个时长一天的课程使他们意犹未尽，听了还想听。*我们很抱歉实在没有能力满足所有人的需求，特别是有些地方的预约人数过少，我们协调起来实在比较麻烦。
在另一方面，有些朋友没法跟老板请假腾出一整天时间来参与课程，他们可能也更喜欢在工作后的碎片时间，在自己舒适的卧室里深入了解Neo4j。
所以我们制作了这个在线版本的教程来让您求教若渴的内心得到心灵食粮的滋润，同时也方便我们得到及时的反馈：如果你觉得哪里讲得不够好，请不留余力地向我们<a href="mailto:training@neotechnology.com" >吐槽</a>。
你可以随心所欲地暂停和恢复课程进度，只要输入你的邮箱地址重新登陆就成。

##这课怎么上？

我们把教程分为了好几课，每一课大概是两小时，也就是一节晚自习的时间，你可以在工作后的傍晚毫无压力地享受这知识的盛宴。如果你遇到不懂的词，请继续往下看，在翻译过程中我发现前半部分莫名其妙的词在后期都得到了诠释，实在令人越看越释然。

###这些课程包括了
1.对图数据库和Neo4j的简介
2.Cypher入门 - 一种神奇的图查询语言
3.用Cypher来创建和编辑一个图
4.Neo4j实战
你可以通过在左边或在左上角（如果你用的是小屏幕设备）的课程目录直接查看各节课和各章节。
在课程中我们通过几种不同的方式来提供信息：
-我们机智的主持人Andreas Kollegger为大家带来的有趣的视频课
-带有实实在在的例子的用法释义
-在我们之前开过的课上用的展示各种材料的幻灯片
-特殊福利：一个交互查询小部件，Cypher Query Gadget，你可以用它来尝试使用Cypher语言并立即得到交互反馈，从而深刻领悟Cypher
之奥义

##传说中的Cypher Query Gadget

你可以随意玩弄玩弄这个小部件，用Cypher进行交互操作，而且她很难被玩坏，所以任性地玩弄她吧！只要你能从中学到东西，她一定会很开心的。当然，如果她真的被玩坏了，你只需要把临时数据库重置一下就好了，她会忘掉一切折磨，重新笑脸相迎。
取决于不同的任务，不同的小部件置有不同的数据集，但是肯定会在课程之间保留你所做的变动。对于大多数的实验操作我们都添加了有用的检查点和信息来指引你。
查询结果会以一个表格视图呈现，如果你请求返回给你节点或是关系的话，它们会在图可视化界面中的数据集里被高亮出来。（看下面的示意图）复杂的元素会在表格里被折叠，点一下就可以展开。
你的历史查询语句在学习过程中都可以再次调用，我们也为你提供了一些预置的查询语句让你可以快速开始尝试。
请尝试独立完成作业，少用复制黏贴！思考思考之后，看看能不能默写出来运行，一点一滴扩充语句，这会让你快速开悟。
<img src="https://static.versal.com/restapi/assets/ed2055eb-83b5-486c-93d6-5df86f228e09" />

##电影和演员图数据模型

我们在整个课程中所使用的数据模型应该是很容易理解的。
我们的目标将是学习图和Cypher，而不会是一个全新的<a href="http://bit.ly/1b70TJp" >领域</a>。为了学习图中的元素，请看向下面这张图：

<img src="https://static.versal.com/restapi/assets/bec5e899-d075-4005-8d45-eee5f1713439" />

数据集包含了：
-带有 ：Movie 标签的电影，它还有 title、released 和 tagline 等属性
-带有 ：Person 标签的人，它还有 name 和 born 等属性
-从演员到电影的一个 ACTED_IN 关系，它还有一个 roles 属性，包含了所饰的角色名
-一个从导演到电影的 DIRECTED 关系

######请牢牢记住这个模型，你会经常用到它。

---------------------

#课程重点和对您的欢迎辞

##欢迎您入门Neo4j

我们很高兴看到你参与我们的在线课程，来增加与Neo4j和Cypher查询语言的亲密度，你可以随时出门和女票约个会然后回来恢复进度，有讲得不好的地方请随意<a href="mailto:training@neotechnology.com" >吐槽</a>。
现在我们的主持人Andreas Kollegger （俺的猎鸭师·扣哥）冲了过来向你问好，向你介绍Neo4j，拔直一点！

Video

图搜索在Facebook中起重要作用，Google也有它的知识图谱，Twitter也使用图论来为用户推荐关注。已经有这么多的大型公司在无数事务上使用图，囊括了推送、物流、生物信息学、网络管理等等。现在还来得及，你，也可以掌握图的力量，对你的数据为所欲为地进行建模、查询，而不需要自己写一个数据库出来。
在这个课程中，你将会学习到图的强大之处，将你的数据联系起来，挖掘出更多的价值。Cypher查询语言将声明式查询语言的优点带入图数据库，比起如何实现，我们可以更多地关注计算机该做什么，而Neo4j图数据库引擎将使你即使在便宜的烂电脑上也可以快速地运行查询语句。
上吧少年！我们从这样一个问题开始：图数据库好处都有啥？谁说对了就给他。

---------------------

#第一课：使用图数据库和Neo4j的理由


许多人认为图数据库就是用于处理社交关系的，但是随着年岁的增长，你会领悟到图数据库越来越多的使用方式，从电商到内容管理，从路由选择到生物信息学，无数的领域都在图数据库的裙下臣服。
在我们为此而骄傲之前，我们先来看看一些关键术语，它们将帮助我们更好地了解图数据库是如何工作的，并让我们对Cypher进行管中窥豹，见此图查询语言之一斑。

小节结束:
(使用图数据库和Neo4j的理由)-->(我们说的图是什么)

---------------------
#(L1)--(我们说的图是什么？)


「图数据库」将数据存储在「图 Graph」这个最基础的数据结构里面，从而可以优雅地，以高可用的方式展示任何种类的数据。
图数据库中的记录叫做「节点 Nodes」。
「节点」通过有向的、有类型的「关系 Relantionships」连接在一起。
每个节点和关系都可以携带一些自己的带名字的特征，也就是我们熟悉的「属性 Properties」。
而「标签 Label」是用于把节点归类到「组 Groups」里的。

##让我们来描述一个图：开始介绍Cypher

来看看我们可以如何使用很基础的Cypher语法来描述一个图里的元素，熟悉一下这传说中的，Neo4j的查询语言。

Video

##节点和关系

在Neo4j里，图是由「节点」和「关系」组成的，而在这个课程内我们将会使用一个关于电影的图，节点将被用于表示像是演员、导演和电影这样的实体，而我们用关系来连接他们。所以我们可以眼睁睁地看着「Keanu Reeves」和「The Matrix」就这么被一个「ACTED_IN 关系」连接在了一起，然后「Clint Eastwood」和电影「Unforgiven」则由「DIRECTED 关系」有了羁绊。

###节点
(a) actors 演员
(m) movies 电影
( ) 阿猫阿狗节点

###关系
-[r]-> 一个叫做「r」的关系
(a)-[r]->(m) 演员通过一个叫做「r」的关系与电影联系在一起
-[:ACTED_IN]-> 这个关系的类型是 ACTED_IN
(a)-[:ACTED_IN]->(m) 演员 ACTED_IN 某些电影
(d)-[:DIRECTED]->(m) 导演 DIRECTED 某些电影

##属性

节点和关系都可以拥有属性。属性通过 key:value 键值对的形式来描述节点和关系，例如一个演员可能会拥有一个 name 名字属性，更可能拥有一个 born 出生年月属性。而一个电影则会拥有 title 标题、tagline 宣传语和 date 发行日期属性等等。

###节点和属性
(m {title:"The Matrix"}) 一个带有标题属性的电影
(a {name:"Keanu Reeves",born:1964}) 一位带有名字和出生年的影星
###带有属性的关系
(a)-[:ACTED_IN {roles:["Neo"]}]->(m) 关系 ACTED_IN 附加上了一个 roles 属性（一个角色名数组）

##标签

我们为了区分不同种类的节点，可以给节点加上「标签 Labels」。任何节点都可以附上一个或好几个标签，比如很明显地，我们可以使用「Movie」、「Person」这样的标签，之后我们可能也会想给 Clint Eastwood 和 Keanu Reeves 加上「Actor」标签，而 Clint Eastwood 又同时可以加上「Director」标签。

###标签
(a:Person) 一个Person
(a:Person {name:"Keanu Reeves"}) 一个带有属性的Person
(a:Person)-[:ACTED_IN]->(m:Movie) 一个ACTED_IN一些电影的Person

小节结束
(我们说的图是什么)-->(传说中的Neo4j)
---------------------
#(L1)--(传说中的Neo4j)


##Neo4j是一个图数据库

-Neo4j是一个数据库，它可以用来可靠地存放和查找信息
-Neo4j的数据模型是基于图的，更准确地说，基于 Property Graph 属性图
-Cypher是Neo4j的图查询语言（用来查询图的SQL！）
-Cypher是一个声明式查询语言
-你可以把重心放在描述你对「什么」感兴趣，而不是操心查询是「如何」办到的
-Cypher想来应该是有着很高的可读性而且富于表现力的吧

Video

##属性图

<img src="https://static.versal.com/restapi/assets/fdc05cea-e18b-44ea-8ba9-e119d7a8f872" />

###请深深地记住这张图片，或者把它画在显眼的位置比如你的脑门上，因为它描述了这个教程中所使用的最主要的模型
-一个 Property-Graph 属性图包含了带标签的节点和各种关系，它们全都带有形形色色的属性
-节点都是 data records 数据记录，一般被用于表示一个实体（加上标签）。除了属性以外，节点也同时具有到其他节点的关系
-一个关系可以连接两个节点，关系也是图数据库中一种明确的数据记录。想像它们包含了两个实体之间的共享信息，比如关系的方向（暗恋是有方向的）、类型（狂躁型暗恋）、属性（暗恋已经5年了），并表示两个节点是被连接在一起的
-属性是简单的 key:value pairs 键值对，不存在什么 schema 容器，这只是一种结构而已
属性是可以让你在Neo4j中存储信息的键值对，而且可以添加到任何节点和关系上，通过node.property和relationship.property来访问。
比如说，一个 actor 节点可能会有一个 name 属性，你可以通过 actor.name 这样的点记号来设置它,以及在将来使用其中存储的内容。一个影星自然会在电影中饰演角色，所以我们可以使用 ACTIN_IN 类型的关系来连接他和电影。如果我们对这个关系添加一个标识符 acting ，我们就可以在将来通过 acting.roles 取得他这次出演中的角色属性。
属性可以是布尔变量、数字、字符串等简单类型，也可以是一个简单类型组成的数组。在<a href="http://docs.neo4j.org/chunked/stable/graphdb-neo4j-properties.html" >Neo4j 文档</a>中可以查到更多信息。

##免费海报下载！
<img src="https://static.versal.com/restapi/assets/5e235aa2-eb33-4a34-a927-46db504e2530" />
<a href="http://bit.ly/1EVrJFF" target="_blank">图数据库是啥！</a>
既然我们已经领会了Neo4j图中的基本元素，我们就可以一起用Cypher来玩一票了。

小节结束
(我们说的图是什么)-->(开始使用Cypher)
---------------------

#(L1)--(开始使用Cypher)


Video

##Cypher是Neo4j的图查询语言

Cypher是一个 declarative 声明式查询语言，也就是说它无需表达程序控制流程，只需表达逻辑计算即可，也就是仅仅表达应该干什么，而不是详细说明如何去完成它。 Cypher不但吸收了SQL的优势元素，而且可以描述图的模式。它使我们可以描述我们想在图数据库里 select 选择什么、insert 插入什么、update or delete 更新或删除什么，而不需要我们操心我们到底要如何做这些事。
<img src="https://static.versal.com/restapi/assets/30fdefb9-b20a-4359-a682-5664b820fd3f" />

##节点

Cypher使用 <a href="http://en.wikipedia.org/wiki/ASCII_art">ASCII art</a>来表示模式。我们用圆括号来包住一个节点，看起来好像画了个圆圈一样：(node) 。如果我们接下来想要提及这个节点，我们将会给它一个标识符，例如(a) 是一个 actor 影星 ，(m)是一个 Movie 电影。在实际操作中，我们应该使用一个长一点而又好记的变量名，比如(actor)和(movie)。
所以如果我们想要找出所有的影星和他们参演的电影，可以构造一条包含了 actor 和 movie 标识符的查询语句，比如用 (actor)-->(movie) 这样的模式查出它们后，我们就可以接着用 actor.name 和 movie.title 来进一步获取他们的隐私信息。
更基本的结构是这样的：
<pre>
MATCH (node) RETURN node.property

MATCH (node1)-->(node2)
RETURN node2.propertyA, node2.propertyB
</pre>

##关系

我们可以从上面那段Cypher片段中发现一点问题：我们没有对两个节点之间的关系做任何说明，所以即使我们使用了标识符 actor ，我们还是有可能取得与电影相关联的导演和制片人，因为 actor 只是类似一个形式参量。所以我们需要在查询的时候描述一下关系的类型。
如果我们想要取得在一个电影里出演的每一个人，我们会描述这样一个模式：(actor)-[:ACTED_IN]->(movie) 以便让查询只返回与其他节点（电影节点）有着一个 ACTED_IN 关系的节点，也就是 actors ，ACTED_IN 关系暗示了这样的节点只会是 actors 。
或是更普遍地：
<pre>
MATCH (node1)-[:REL_TYPE]->(node2)
</pre>
顺便一提，REL_TYPE是Relationship Type 的缩写。
有时候我们需要用到关于某个关系的信息，比如说它的类型或是属性，我们可能会想要输出一个影星在电影里所饰演的角色，而这个角色名就可以是影星与电影之间的 role 关系的一个属性。与节点一样，我们可以给关系添加一个标识符，添加在表示类型的 :TYPE 前面。那么如果我们让数据库去匹配 (actor)-[role:ACTED_IN]->(movie)，就可以对每一个影星再用 role.roles 查看他们在每一个电影里所饰演的角色了。
<pre>
MATCH (node1)-[rel:TYPE]->(node2)
RETURN rel.property
</pre>

##标签
标签让我们可以给节点编组。例如，我们可能想要区分有关人的电影和有关动物的电影（很不幸都是乡村田园片），通过 MATCH 匹配 (actor:Person)-[:ACTED_IN]->(movie) ，我们将会得到“城市牛仔”克林特•伊斯威特 Clint Eastwood ，而不是 Clyde ——他在《永不低头 Every Which Way but Loose 》中的宠物猩猩。不过我们可别现在开始讨论他们俩谁在电影发烧友中享有最高的荣誉……
标签一般是这样使用的：
<pre>
MATCH (node:Label) RETURN node

MATCH (node1:Label1)-[:REL_TYPE]->(node2:Label2)
RETURN node1, node2
</pre>
为了更直观地解释上述观点，我们试着在一个影星和电影领域的模型中搞出了一个具体一点的查询，找出所有符合条件的影星的名字，以及他们在电影《黑客帝国》中所饰角色。

Slideshow

值得一提的是，虽然节点的标签和关系的类型都是用 :blabla 表示的，但是一个叫做「标签 Label」一个叫做「类型 Type」，接下来我们会看到，它们使用的方式也是有点儿不同的。

小节结束
(开始使用Cypher)-->(开始写第一条Cypher查询语句)
---------------------

#(L1)--(开始写第一条Cypher查询语句)


现在我们已经浏览过Cypher语言的一些小片断，是时候展现真正的实力了，我们来瞅瞅这些虽然简单，却完整而威力永恒的Cypher查询语句。

Video

##一些简单的查询语句

请试着在下面的Cypher widget小部件里试一试这些查询语句，勤能补拙，聪明的你明白我的意思了么？因为直接复制黏贴会降低智商，使理解能力减退，所以请试着自己输入语句。

###查询所有节点
如果我们想要返回图里的每一个节点，我们可以使用如下查询语句：
<pre>
MATCH (n)
RETURN n;
</pre>
这个查询语句会搜索整个图，遍历每一个节点，看看它是否满足模式 (n) ，也就是一个可能带关系也可能不带关系的节点，所以自然每一个被遍历的节点都满足这个模式。而 RETURN 字句将会返回每个节点的信息，包括它们的属性。
注意 RETURN 子句后面的分号，这是用来告诉 Neo4j-Shell 你已经写完了查询语句。在 Neo4j-Shell 的命令行中，如果你不使用分号，Neo4j将会猜测你还没写完语句，并静候汝行，想看看你接下来还想写什么。不过在本课程内使用的Cypher gadget内以及 Neo4j Browser内就不需要加分号了，因为它们被默认忽略。

##两个节点，一段关系

现在我们想要返回所有与另一个节点有关系的节点。这依然是要返回每一个带关系的节点——还带着另一个小伙伴节点，但这让我们朝着一个蛮重要的方向迈出了一步，所以请保持耐心！
为了描述这样的查询目标，我们这样写查询语句：
<pre>
MATCH (n)-->(m)
RETURN n, m;
</pre>
这将会返回每一个点对，他们的顺序表明了在图里有一个从n到m的关系。

##不是所有东西都需要名字

如果我们只是想要返回一个和其他另一个节点有着一段关系的节点，而不关心这另一个节点是什么鬼，我们可以通过不写第二个节点的标识符，而只写起始节点的标识符来构造查询语句：
<pre>
MATCH (n)-->( )
RETURN n;
</pre>
万万不可忽略的是我们去掉了 RETURN 子句中的 m ，也去掉了 MATCH 字句中的 m 。

##返回属性

在实际操作中，大多数时候我们都不会想要返回整个节点，我们只是想知道与它有关的个别信息而已。如果我们只想知道一坨节点的名字而已，我们就应该这样做：
<pre>
MATCH (person)-->()
RETURN person.name;
</pre>
在我们当前使用的数据集里，这样的查询是有意义的，因为在此，任何人（带有 name 属性）都有一个指向电影的关系，如果不是这样的话，在别的数据集里我们很可能会返回得到一大堆 NULL 。
更一般地：
<pre>
MATCH (node)-->()
RETURN node.property;
</pre>
注意我们可以使用任何我们觉得方便的标识符，来使这个语句的内容看起来比较有意义，而这个标识符是叫做 a,n,m 还是 person 都无所谓，因为它只是类似一个形式参量，我们只要保证它易读且可为人类所理解就行。

##查找一段关系

在Neo4j中有两个保证数据完整性的限制：
1.一个关系必须有起始节点和终止节点（是的，它们可以是同一个节点）。所以 dangling relationships 像钟摆那样一根线连着一个节点的关系，以及 broken links 光秃秃一根线，都是不能有的。
2.所有关系都必须有一个类型。
我们可以给关系任命一个标识符，这样我们就可以返回关系的属性和类型了。
<pre>
MATCH (node)-[rel]-> ()
RETURN node, rel.property;

MATCH (node)-[rel]->()
RETURN node, type(rel);
</pre>
在我们的电影图模型里：
<pre>
MATCH (person)-[rel]->(movie)
RETURN person, type(rel)
</pre>
依惯例上述关系的类型应该用大写字母和下划线写就，所以上面语句的结果应该是 DIRECTED,，ACTED_IN 和 PRODUCED。

##匹配出一段关系

目前为止，大多数我们写出来的查询语句都会返回太多太多的结果，而一般来说我们不会想要看完繁浩如此的图里的所有节点，或是图里所有带关系的节点。我们很可能想要的是所有「在某个电影里出演过」的人，看看他们的名字，以及这些电影的标题。我们可以这样构造查询语句：
<pre>
MATCH (actor)-[:ACTED_IN]->(movie)
RETURN actor.name, movie.title;
</pre>
注意上面在我们想要匹配的关系类型之前有一个冒号，这使得我们依然可以在前头添加一个标识符，以便返回它的属性：
<pre>
MATCH (actor)-[role:ACTED_IN]->(movie)
RETURN actor.name, role.roles, movie.title;
</pre>
对于每一个影星和电影组成的节点对，这条查询语句将会展示出影星的名字、他们在电影里饰演的角色以及他们所参演的电影。
更一般地，我们会这样写：
<pre>
MATCH (node1)-[rel:TYPE]->(node2)
RETURN node1.propertyA, rel.property, node2.propertyB;
</pre>

##通过节点标签来匹配

我们常常会不想遍历图里的所有节点来匹配一个模式——这极其低效腐朽。提升效能的第一步：通过使用标签来限制我们要遍历的节点集。下面展示的查询语句将会让我们看到如何返回在一个电影里参演的每个人，但是它只会在人族里面查找，所以首先它不会把参演的宠物大猩猩也囊括进搜索范围，其次它不会再直接地从表示电影的节点这样非人哉的节点开始查找了，因此可以秀出班行地提升查找效率：
<pre>
MATCH (actor:Person)-[:ACTED_IN]->(movie)
RETURN actor.name, movie.title;
</pre>
接着如果我们想要查找某一个特定的人——带着 Person 标签的人有很多，但我们只要这一个——我们可以将他和一个属性上的限制放在一起，使用 WHERE 来查找 Tom Hanks：
<pre>
MATCH (tom:Person)
WHERE tom.name = "Tom Hanks"
RETURN tom;
</pre>
注意到我们依然遍历了图中所有的人族成员，但是我们并没有遍历整个图和其他无关节点。
下面是一个更短的语法结构，用来通过属性的值查找节点：
<pre>
MATCH (tom:Person {name:"Tom Hanks"})
RETURN tom;
</pre>

##轮到你来试用Cypher啦～在下面的互动窗口里，玩一玩上面提到的查询语句吧！

Gadget

#Cypher查询语言是通过好几个子句组合而成的

##查询图
MATCH：在这用某个模式来查找图数据库
WHERE：在这使用谓词过滤结果，也可以限定住组成模式的某个元素
RETURN：在这返回并给出结果集，同时可以在这做聚合
ORDER BY：在这对查询结果进行排序
SKIP/LIMIT：在这对查询结果进行分页
##更新图
CREATE: 新建节点和关系
MERGE: 单一地新建节点
CREATE UNIQUE: 单一地新建节点
DELETE: 删除节点或关系
SET: 更新属性和标签
REMOVE: 移除属性和标签
FOREACH: 对一个列表中的每一个元素进行更新
WITH: 把查询行为分割成好几个不同的片段，把前一个的查询结果当作下一个查询语句的一部分

现在我们一起把「Find all Characters in the Movie "The Matrix" 」这句话和下面这些我们已经学会的概念对应起来：
-Nodes
-Relationships
-Properties
-Relationship
-Types
-Labels
-Identifiers
我们一步一步来，这活儿多简单呀老伙计，我敢向老天爷发誓，我们不会让事儿变得更简单啦！

Slideshow

目前为止我们还只会处理简单的模式，接着来看看Cypher是怎么搞定更长的路径的。

小节结束
(开始写第一条Cypher查询语句)-->(Cypher中的路径)
---------------------

#(L1)--(Cypher中的路径)

一条「路径 Paths」是一系列连接在一起的节点和关系。路径也可以通过模式来匹配查找。

Slideshow

那么我们可以用这些路径来做什么呢？这么说吧，我们如果想要一个一个地分别展示与每位演员们合作过的导演，我们可以像下面这样写查询语句：
<pre>
MATCH (actor)-[:ACTED_IN]->(movie)<-[:DIRECTED]-(director)
RETURN actor.name, movie.title, director.name;
</pre>
它可以查找出所有 (actor) 以及跟他们由 ACT_IN 关系而连接在一起的 (movie) ，同时语句还查找 (director) 所 DIRECTED 的同一部 (movie) ，所以对于每一对有着千丝万缕不可言说的联系的影星和导演，以及使他们发生关系的那部电影，查询语句都会返回结果。如果在一部电影里有十个影星和一个导演，就会返回十个结果；如果电影有十个影星两个导演，那么就会有二十个结果：每个影星与第一个导演的结果加上与第二个导演的结果，是不是很像笛卡尔积？

当我们谈论 (actor) 和 (director) 所代表的影星和导演时，我们在谈论什么？实际上查询语句并不知道关于节点的任何信息，它们只是遍历所有探出 ACT_IN 关系的节点，这个 ACT_IN 关系指向某个节点，以及所有探出 DIRECTED 关系的节点，这个 DIRECTED 关系也指向同一个节点而已。

##其他写法

我们刚写过这样的查询语句：
<pre>
MATCH (actor)-[:ACTED_IN]->(movie)<-[:DIRECTED]-(director)
RETURN actor.name, movie.title, director.name;
</pre>
这样写的确直观好看，但是当我们我们想要匹配一条蜿蜒不绝长得发指的路径时，我们会发现写在一行实在有碍观瞻，所以我们也可以让它分在好几行里：
<pre>
MATCH (actor)-[:ACTED_IN]->(movie),
(movie)<-[:DIRECTED]-(director)
RETURN actor.name, movie.title, director.name;
</pre>
我们把模式匹配语句用逗号隔开，让我们有时间喘口气，但我们还是可以返回同样的结果，因为上面两种写法实际上是等价的，返回的结果集是同样的。
应该注意到我们在两个分开的字句里都写了一遍标示符 movie ，这很重要，因为如果我们不在下面的子句里也注明 (movie) 的话，我们将会得到两个完全不同的结果集，而加上那个 (movie) 之后我们才相当于把两个字句联系在了一起。
当然我们还有其他表述方式：
<pre>
MATCH (actor)-[:ACTED_IN]->(movie),
(director)-[:DIRECTED]->(movie)
RETURN actor.name, movie.title, director.name;
</pre>
我们把第二个字句左右掉了个个儿，:DIRECTED 还在中间没动，而箭头的方向反过来了，还是指着 (movie)。
所以下面两个东西在Cypher看来其实是一个意思：
<pre>
(movie) <-[:DIRECTED]- (director)
(director)-[:DIRECTED]->(movie)
</pre>

###轮到你啦！来尝试一下刚学会的语句吧！

Gadget

(I found that if there are some pictures below the Gadget , after a single running in the Gadget , next inpuit will result in a moving downward ,I don't know why and have tried in IE and Chrome , resulted in same . But if there aren't pictures below , there will be no place to move downward , so I recommend to remove the pictures here ， or move them up. )

小节结束
(Cypher中的路径)-->(Cypher中的路径，更多)
---------------------

#(L1)--(使用路径)

在可以使用路径来匹配模式之后，我们作为程序员很自然地还会想要把这个路径赋值给某个变量，给它取个好听的变量名，然后再返回它的结果，就像下面这样，我们重写了刚用过的那个语句：
<pre>
MATCH p=(a)-[:ACTED_IN]->(m)<-[:DIRECTED]-(d)
RETURN p;
</pre>
这将会返回每条路径中的所有节点和关系——包括它们身上所有的属性。这很有意思对吧，但它可能会返回过多的信息，所以我们可能会像下面这样，使用 nodes()函数，让它只返回路径中的节点：
<pre>
MATCH p=(a)-[:ACTED_IN]->(m)<-[:DIRECTED]-(d)
RETURN nodes(p);
</pre>
只需要返回关系的同志可以试试这个:
<pre>
MATCH p=(a)-[:ACTED_IN]->(m)<-[:DIRECTED]-(d)
RETURN rels(p);
</pre>
注意只有连着写的一长句查询语句才能把想查的路径模式赋值给某个变量，如果你在MATCH字句里像下面这样写了两个模式，你需要分开返回两个不同的变量：
<pre>
MATCH p1=(a)-[:ACTED_IN]->(m), p2=(d)-[:DIRECTED]->(m)
RETURN p1, p2;
</pre>

##Cypher查询窗口
Gadget

小节结束
(Cypher中的路径，更多)-->(图·实验室)--(路径)
---------------------

# (图·实验室)--(路径)

我们已经见识过如何返回一部电影里所有的影星和导演了：
<pre>
MATCH (a)-[:ACTED_IN]->(m)<-[:DIRECTED]-(d)
RETURN a.name, m.title, d.name;
</pre>

##这个查询语句怎么修改才可以只返回在自己执导的电影里出演的奇葩导演呢？
Cypher Gadget下面给了你一些提示：
Gadget
(oh my gosh ,after my typing I moved down and saw the hint , can this be fixed? )

提示：如果你遇到问题的话，看看如果你把 (d) 和 d.name 用 (a) 和 a.name 替换掉会发生什么？成么？它的原理是什么？你可以如何简化它？

小节结束
(图·实验室)--(路径)-->(图·笔记)--(图的历史)
---------------------

#(图·笔记)--(图的历史)

当我们谈论图的时候，我们谈论的是一种展现信息的方式，源于数学的那种，也就是说这个图的意思不是图表、不是图例、不是黄图，图数据库的意思也不是专门用来存储同人本的数据库，希望你不是直到你现在才明白……之前的「图」都是「图论」的缩写而已。
那么…让我们来看看图论，了解一下它的历史会让你受益良多。

##莱昂哈德·欧拉 Leonhard Euler

让我们一起膜拜Leonhard Euler大神，瑞典数学家，13岁时入读巴塞尔大学，15岁大学毕业，16岁获得硕士学位，发明了大多数现代数学中用的术语记号，比如方程记号之类，并且是图论的先驱，长得仙风道骨。
<img class="unselectable js-image" unselectable="on" src="//static.versal.com/restapi/assets/fbe23546-90ea-4444-9366-733296375ad9" style="left: 0px; top: 0px; width: 723px; max-width: 723px; height: 406.6875px; max-height: 406.6875px;">
Euler在柯尼斯堡住过一段时间，并于1736年创造了然后又解决了「七桥问题」。

##七桥问题

<img class="unselectable js-image" unselectable="on" src="//static.versal.com/restapi/assets/709a92ed-bcf3-45b1-9534-13fb897a89b7" style="left: 0px; top: 0px; width: 723px; max-width: 723px; height: 406.6875px; max-height: 406.6875px;">
这个问题问的是有没有可能从某个点出发刚好把每座桥都过一遍并回到出发点，这个问题是图论研究的开始。
##幻灯片：一次走过柯尼斯堡的七座桥
Slideshow

##由此得到的结构就叫做图

对于正常人类(而不是数学家)，我们发现使用术语「节点 Nodes」和「关系 Relationship」能让事情更简单美好一点。
<img class="unselectable js-image" unselectable="on" src="//static.versal.com/restapi/assets/86e30d34-d59d-4093-83ac-6c0d9b9c79bd" style="left: 0px; top: 0px; width: 718px; max-width: 718px; height: 403.875px; max-height: 403.875px;">

小节结束
(图·笔记)--(图的历史)-->(在充满关系的世界中使用图)
---------------------

# (图·笔记)--(在充满关系的世界中使用图)

##关系型数据库
你肯定听说过关系型数据库，它们将数据存放在表里，使用行和列来组织数据，就像Excel做的电子表格一样。每一列都有名字和类型，每一行都表示一个条目或是实体，通过附加约束和事务的ACID来保证一致性。
在查询时，关系型数据库通过匹配的主键和外键来计算表之间的关系，这些关系是通过外键约束来表明和生成的而且只能用于无选择性的关系。如果你想要表示一个更高的基数 (m：n) ，你就会需要一个额外的中间件 JOIN-TABLE 来连接两个表。
而带有多个连接操作的SQL查询会变得非常消耗资源，因为它需要计算许多关系。
幻灯片：在关系型数据库里连接数据
Slideshow

##关系图中的自然形成的路径
查询——或遍历——图涉及以下几个途径，由于Neo4j非关系型数据库中的数据模型是从根本上面向路径的，所以大部分基于路径的数据库操作都和数据配合完美，使其效率极高，性能卓超。

###参考
Ian Robinson, Jim Webber, and Emil Eifrém. Graph Databases . O'Reilly Media, 2013, p. 20

小节结束
(图·笔记)--(在充满关系的世界中使用图)-->(回顾第一课)
---------------------

#(L1)--(回顾第一课)
Video
当Google和Facebook这样的公司需要费大力建立自己的图数据库引擎时，你已经可以轻松地通过使用较为成熟、知性的Neo4j来享受图数据库的优势了！
Neo4j将信息存储在包含节点(actor、movie)的图里，并且用关系(ACT_IN、DIRECTED)来连接它们，而且节点和关系都带有键值对属性(影星可以有名字，电影可以有标题，饰演关系可以有影星所参演的角色)。
使用图的奥义在于匹配模式。Cypher功能强大，它吸收了SQL的优点，可以描述性地指定模式并取得图中匹配的模式。
在下一节课里我们会看到Cypher的其他才干，比如更高效地筛选和聚合结果集。

第一课结束
---------------------
#第二课：高级Cypher查询

Video
在这个部分我们将会探索Cypher的其他特性。从基于属性值来使用 WHERE 字句到 ORDER BY 、LIMIT 、SKIP，我们会学到如何写出一个成熟而精美的查询语句。我们还会花一点时间看看Cypher里可以使用的一些聚合命令。

开始第二课(L2)
---------------------
(L2)--(在Cypher中筛选)

Video

Cypher提供给我们一系列用于限制返回的结果集中匹配的模式数量机制。

我们先看看一个简单的例子：
<pre>
MATCH (n:Person)
WHERE n.name = "Tom Hanks"
RETURN n;
</pre>
这将会查找图中所有带着Person标签的节点，而且如果它们中有一个的名字是”Tom Hanks“的话，就会返回这个节点。
下面是一个更短的写法，因为它直接将名字属性加到了 MATCH 子句中的筛选器里。
<pre>
MATCH (n:Person {name:"Tom Hanks"})
RETURN n;
</pre>
##试试！Cypher查询窗口

Gadget

小节结束
(在Cypher中筛选)-->(排序、限制和忽略)
---------------------
#(L2)--(排序、限制和忽略)

##Cypher中的排序
在Cypher中可以像SQL里一样使用 ORDER BY 来对结果排序，比如说我们想要看看最老的影星是谁，我们可以这样做：
<pre>
MATCH (a:Person)-[:ACTED_IN]->()
RETURN a.name, a.born
ORDER BY a.born
</pre>
这次查询将会返回每一个影星(事实上同一个影星在不同的电影里出演过几次就会被返回几次——我们将会在后面讲到的 DISTINCT 聚合中修好这个语句的bug) ，并将他们以出生年月的顺序排列，所以会在最上面展示出最老的影星，因为他们有着最小的出生年月。

#限制和忽略
就像一些SQL数据库里所支持的，Neo4j也支持方便的对结果集分页的功能。使用 LIMIT 和 SKIP 语句来减少返回的结果的数量并可以对结果分页。
那么如果我们想要展示第二页的影星和他们参演的电影，我们可以这么写：
<pre>
MATCH (a)-[:ACTED_IN]->(m)
RETURN a.name, m.title
SKIP 10
LIMIT 10;
</pre>
我们也可以只用 LIMIT 来实现只显示结果集中前n个元素的功能，想看看库中最老的五个影星是谁么？
<pre>
MATCH (p:Person)
RETURN p.name
ORDER BY p.born
LIMIT 5;
</pre>
##试着对查询结果做一些分类和限制吧！

Gadget

小节结束
(L2)--(排序、限制和忽略)
---------------------

#(L2)--(用Cypher来探索图)

在这个视频里我们将会学到如何用Cypher来探索一个未知的图，并从中得到其数据结构的一些深刻内涵。

Video

一般来说，标签可以帮助我们了解图中节点的类型。你可以返回一个示例数据集来看看这些节点上的属性和关系一般会是什么。

##不重复的结果
你可能常常只需要返回的查询结果中一些独立的部分，比如说，你想要看看最老的五个影星，一开始你会这样输入：
<pre>
MATCH (a)-[:ACTED_IN]->()
RETURN a
ORDER BY a.born
LIMIT 5
</pre>
但如果其中一名老影星在不止一个电影里出演过的话，你就会好几次搜到他的名字，所以我们真正需要的查询语句是这样的：
<pre>
MATCH (a)-[:ACTED_IN]->()
RETURN DISTINCT a
ORDER BY a.born
LIMIT 5</pre>

##用Cypher来了解人民
既然我们已经会了 DISTINCT 命令，一起来搜一下图中带有Person标签的节点，看看他们之间的关系，了解一下基层人民的现状吧。

Video

##试试这些更复杂一点的查询语句，改变参数看看会发生什么
Gadget

小节结束
(用Cypher来探索图)-->(图·实验室)--(查询电影)
---------------------
#(图·实验室)--(查询电影)

现在一起试着解答一些有趣的问题：如果你想要找到所有 Tom Hanks 参演的电影 ，你会怎么做？
提示：我们想从一个叫做 Tom Hanks 的人出发。

Gadget

###解法之一：
<pre>
MATCH (tom:Person)-[:ACTED_IN]->(movie)
WHERE tom.name="Tom Hanks"
RETURN movie.title;
</pre>
很好，如果你想要限制一下，我们只查2000年以后出的电影呢？(电影有一个叫 Released 的属性)

###解法之一：
<pre>
MATCH (tom:Person)-[:ACTED_IN]->(movie)
WHERE tom.name="Tom Hanks"
AND movie.released > 2000
RETURN movie.title;
</pre>

##我们来看看另一个人……Keanu Reeves

Gadget

###Keanu Reeves 参演了什么电影？

###解法之一：
<pre>
MATCH (keanu:Person)-[:ACTED_IN]->(movie)
WHERE keanu.name = "Keanu Reeves"
RETURN movie.title;
</pre>
很好，那么怎么查他在其中饰演了 Neo 这个角色的电影呢？
-提示：你需要给关系加个标示符 identifier
-提示：ACTED_IN 关系有一个 roles 属性，而且是一个数组
-提示：查看一个元素有没有在数组内的语法是 {element) IN r.roles
##语法指导
<pre>
{expression} IN {collection}
</pre>
检查 {expression} 中的值是否存在于 {collection} 中。
###解法之一：
<pre>
MATCH (keanu:Person)-[r:ACTED_IN]->(movie)
WHERE keanu.name="Keanu Reeves"
AND "Neo" IN r.roles
RETURN movie.title;
</pre>

小节结束
(图·实验室)-->(用比较操作符来筛选)
---------------------
#(L2)--用比较操作符来筛选)

我们可以通过比较不同节点的属性来进行海选，比如说我们可以返回所有与 Tom Hanks 共演过却比他年长之人：
<pre>
MATCH (tom:Person)-[:ACTED_IN]->()<-[:ACTED_IN]-(a:Person)
WHERE tom.name="Tom Hanks"
AND a.born < tom.born
RETURN a.name;
</pre>
注意我们不用多余地在中间的括号里加一个 movie ——一个 () 就够了，因为我们并不需要查询所有他们一起共事过的电影的名字。
我们还可以向 RETURN 子句中添加一些数学运算，并为其取一个名字，从而可以得知他们差几岁：
<pre>
MATCH (tom:Person {name:"Tom Hanks"})-[:ACTED_IN]->(movie),
(movie<-[:ACTED_IN]-(a:Person)
WHERE a.born < tom.born
RETURN DISTINCT a.name, (tom.born - a.born) AS diff;
</pre>
数学运算可以添加在 RETURN 和 WHERE 中。

##试着把这些运算用上，并试试别的运算方法

Gadget

小节结束
(用比较操作符来筛选)-->(用模式来筛选)
---------------------
#(L2)--(用模式来筛选)

现在我们已经学会了在 MATCH 字句中使用路径作为一种筛选模式，我们其实还可以把路径用在 WHERE 子句中，作为一种筛选表达式。

##如果我们想看看影星 Gene Hackman怎么办？
<pre>
MATCH (gene:Person)
WHERE gene.name="Gene Hackman"
RETURN gene;
</pre>
或用另一种更简明的语法：
<pre>
MATCH (gene:Person {name:"Gene Hackman"})
RETURN gene;
</pre>
##我们要怎么找到所有与 Gene 同事的影星?
<pre>
MATCH (gene:Person)-[:ACTED_IN]->()<-[:ACTED_IN]-(other)
WHERE gene.name="Gene Hackman"
RETURN DISTINCT other;
</pre>
##现在，我们怎么从中筛选出一个同时导演着自己这部电影的奇葩？
<pre>
MATCH (gene:Person)-[:ACTED_IN]->(m),
(other)-[:ACTED_IN]->(m)
WHERE gene.name="Gene Hackman"
AND (other)-[:DIRECTED]->()
RETURN DISTINCT other;
</pre>
##这有一个更复杂的例子——一个影星与 Tom Hanks 共事过但 Robin Williams 在的时他就不出演。
<pre>
MATCH (gene:Person {name:"Gene Hackman"})-[:ACTED_IN]->(movie),
(other)-[:ACTED_IN]->(movie),
(robin:Person {name:"Robin Williams"})
WHERE NOT (robin)-[:ACTED_IN]->(movie)
RETURN DISTINCT other;</pre>
##下面这个小小的数据集并不影射现实，只是供你试一试上面的查询语句的。

Gadget

小节结束
(用模式来筛选)-->(图·实验室)
---------------------
#(图·实验室)--(所有黑客帝国中的角色)

那么，现在使用我们已经学过的语法，返回电影黑客帝国 The Matrix 中的所有角色。
提示：电影都带有标签 Movie 和一个你想要用于比较的 title 属性
提示：我们在搜的是角色名(ACT_IN 关系中的属性 roles) 而不是影星的名字

Gadget

如果你得到了所有想要的结果，证明你的智力水平足够让你脱单了。
---------------------
(L2)--(索引和标签)

Video

就像其他的数据库，Neo4j在你添加了索引的情况下会工作得更好。一般来说你会想创建一个「标签特异性索引 label specific index」，也就是只与特定种类的节点相关的索引。
那么如果你想要更高效地通过标题来搜索电影，你可能会运行下面这样的Cypher命令：
<pre>
CREATE INDEX ON :Movie(title);
</pre>
你觉得该怎么创建一个用名字来搜索人的索引呢?
##解答
<pre>
CREATE INDEX ON :Person(name);
</pre>
而且在实际搜索过程中，你根本不需要在查询语句里使用这些索引，就像下面这样，像以前那样直接搜就行：
<pre>
MATCH (gene:Person)-[:ACTED_IN]->(m),
(other)-[:ACTED_IN]->(m)
WHERE gene.name="Gene Hackman"
RETURN DISTINCT other;
</pre>
这个针对 Gene Hackman 的搜索将会运行得风驰电掣——尽管在较小的数据集中不是很看得出来就是了。
##试着不用索引搜一次再用索引搜一次

Ganget

小节结束
(索引和标签)-->(聚合)
---------------------
#(L2)--(聚合)

Cypher也提供了聚合机制，与SQL中的 GROUP BY 相似但是更强那么一点。

Video

就像SQL，Cypher提供了对多种聚合功能的支持：
Count(x) 计算事件数量的和
min(x) 取最低的值
max(x) 取最高的值
avg(x) 取几个值的平均
sum(x) 把几个值加起来
collect(x) 把所有值放进一个集合

更多聚合函数可以在<a href="http://docs.neo4j.org/chunked/2.0.0/query-aggregation.html#_statistics" target="_blank">Neo4j 手册</a>中找到。
下面我们用聚合来得到一个影星所参演的所有电影的清单，注意结果中 title 那一列现在会展示很长一列数组了：

Video

##收集
比如说我们现在想要展示一个影星所参演过的所有电影的标题，又称为xxx全集 filmography，我们可以使用下面这条语句：
<pre>
MATCH (a:Person)-[:ACTED_IN]->(m)
RETURN a.name, collect(m.title);
</pre>
对于至少参演过一场电影的人，这条语句都会返回他们的名字，并且把他参演过的电影名放在一个字符串数组里。
我们一起来看看 Tom Hanks 的电影。

Video

下面是更多例子。
比如说你要怎么返回每个影星以及所有分别与他们共事过的导演之名？
###解法之一：
<pre>
MATCH (a:Person)-[:ACTED_IN]->(m)<-[:DIRECTED]-(d)
RETURN a.name, collect(d.name);
</pre>
##加和
举例说我们想要返回一个影星所参演过的电影数量，我们要怎么搜？
###解法之一
<pre>
MATCH (a:Person)-[:ACTED_IN]->(m)
RETURN a.name, count(m);
</pre>
##
在使用了 count 聚合之后，我们经常会对排名前几位的结果感兴趣。这好奇心可以通过先用 count 加和得到结果总数，然后用 DESC 结尾，再用 LIMIT 限制只显示前n个。举个形象的例子，如果我们对出演电影数量排名前十的影星感兴趣，我们可以这样搜：
<pre>
MATCH (a:Person)-[:ACTED_IN]->(m)
RETURN a.name, count(m)
ORDER BY count(m) DESC
LIMIT 10;
</pre>
那么影星和导演合作过的电影数量排名要怎么看？
###解法之一
<pre>
MATCH (a:Person)-[:ACTED_IN]->(m)<-[:DIRECTED]-(d)
RETURN a.name, d.name, count(m);
</pre>
##动手实践记忆好！

Gadget

小节结束
(聚合)-->(图·实验室)
---------------------
#(图·实验室)--(聚合：加和)

试着想出一个可以展示出参演电影数最多的五名杰出影星的查询语句。
提示：使用聚合和排序

Gadget

##解法之一
<pre style="color:transparent;user-select:text;-webkit-user-select:text;">
MATCH (a:Person)-[:ACTED_IN]->(m)
RETURN a.name, count(m)
ORDER BY count(m) DESC
LIMIT 5;
</pre>
这个答案隐藏起来了，但是你可以像在萌娘百科里面一样通过选中来查看和复制它。

小节结束
(图·实验室)--(图·实验室·改)
---------------------
#(图·实验室·改)--(聚合：加和)

##推荐三位 Keanu Reeves 应该共事但却还没和他在一起过的影星
我们想说的是一种「朋友的朋友」查询语句，虽然我们的图中没有朋友关系，不过我们有共同出演关系嘛。所以尽管题目陈述会有那么一点啰嗦，我们还是会有不同的推荐算法，记住在图中出现频率最高的前三名人类是 Keanu Reeves 最好的候选人。

Gadget

这个答案隐藏起来了，但是你可以像在萌娘百科里面一样通过选中来查看和复制它。
<pre style="color:transparent;user-select:text;-webkit-user-select:text;">
MATCH (keanu:Person {name:"Keanu Reeves"})-[:ACTED_IN]->()<-[:ACTED_IN]-(c),
(c)-[:ACTED_IN]->()<-[:ACTED_IN]-(coc)
WHERE coc <> keanu AND NOT((keanu)-[:ACTED_IN]->()<-[:ACTED_IN]-(coc))
RETURN coc.name, count(coc)
ORDER BY count(coc) DESC
LIMIT 3;
</pre>

小节结束
(图·实验室·改)-->(复习第二课)
---------------------
#(L2)--(复习第二课)

Video

对于Cypher，我们还有很多可学的，因为它是这么一款威力永恒如狼似虎的语言，而且在<a href="http://docs.neo4j.org/chunked/stable/cypher-query-lang.html" target="_blank">Neo4j 手册</a>和<a href="http://docs.neo4j.org/refcard/2.0/" target="_blank">Cypher作弊卡</a>里有着事无巨细悉究本末的参考资料。但是就算如此，我们也已经学完了Cypher语法中用处良多的一个子集啦！我们已经可以简明地描述出一个强大的查询语句啦！撒花！
来归纳一下Cypher的核心元素：
MATCH：在这用某个模式来查找图数据库
WHERE：在这使用谓词过滤结果，也可以限定住组成模式的某个元素
RETURN：在这返回并给出结果集，同时可以在这做聚合
ORDER BY：在这对查询结果进行排序
SKIP/LIMIT：在这对查询结果进行分页
在下一节课里，我们将会看到如何用Cypher来编辑已有的图。

##第二课结束
---------------------
#第三课-用Cypher编辑图

Video

在这个部分我们将会学到如何依靠 CREATE、MERGE 和 SET 这三个小伙伴，使用Cypher来填入和更新一个图，以及如何与 DELETE 和 REMOVE 相处，来移除节点、关系、属性和标签。

##开始第三课(L3)
---------------------
#(L3)--(通过Cypher创建节点)

我们从向图中添加节点开始吧。

Video

我们一起跟着视频做看看吧，运行一下这些命令，不过把引号里的 My name 换成你自己的名字或是李狗蛋的名字，不过如果凑巧你的英文名和数据库里的国际著名影星重名了，你可能真的得考虑用李狗蛋的名字了：
<pre>
CREATE (me:Person {name: "My Name"}) return me;
</pre>
你将会看到一个新的节点被返回，同时在可视化窗口中浮现出来。你也可以轻而易举地这样检查它唯信息主义哲学上的存在性：
<pre>
MATCH (me:Person)
WHERE me.name="My Name"
RETURN me.name;
</pre>
或是：
<pre>
MATCH (me:Person {name:"My Name"})
RETURN me.name;
</pre>
##那么…现在请创造你自己

Gadget

小节结束
(通过Cypher创建节点)-->(用Cypher添加属性)
---------------------
#(L3)--(用Cypher添加属性)

Gadget

为了继续，请先向数据集中添加一部电影 Mystic River 。
<pre>
CREATE (movie:Movie {title: "Mystic River", released:1993})
RETURN movie;
</pre>
现在比如说我们想要为这部电影添加一个标语，就以属性的形式添加在节点上。
首先我们需要再次定位到刚创建的节点，用它的 title 属性就好，然后就可以用 SET 设置它的属性，下面是例子：
<pre>
MATCH (movie:Movie)
WHERE movie.title="Mystic River"
SET movie.tagline = "We bury our sins here, Dave. We wash them clean."
RETURN movie;
</pre>
因为Neo4j是模式自由 Schema-free的，所以你可以给任何关系任何节点添加任何属性。
那么如果你想要更新一个属性呢？Mystic River 事实上是 2003年上映的，而不是 1993年，我们可以这样修正这个常识性错误：
<pre>
MATCH (movie:Movie)
WHERE movie.title="Mystic River"
SET movie.released = 2003
RETURN movie;
</pre>
所以聪明的你一定发现了添加和更新属性的语法都是一样的：SET 一下 。你 SET 一个属性时，如果它已经存在，那就更新它，如果未有之，则加矣。
#你已经转职电影数据库编辑师成功！

小节结束
(用Cypher添加属性)-->(用Cypher添加关系)
---------------------
#(L3)--(用Cypher添加关系)

在下面的视频中我们将会看到用什么样的格式可以给《黑客帝国》加个影评。

Video

所以，添加一段关系和添加节点是差不多的，但是我们在用 CREATE 创建关系的时候要注意语法: (n)-[:RELATION_TYPE {property:value}]->(m)
一起先在数据库里创造我们自己：
<pre>
CREATE (me:Person {name:"My Name"});
</pre>
然后我们来给电影神秘河 Mystic River 或是其他你想喷一喷的电影评个分：
<pre>
MATCH (me:Person), (movie:Movie)
WHERE me.name="My Name" AND
movie.title="Mystic River"
CREATE (me)-[:REVIEWED {rating:80, summary:"tragic character movie"}]->(movie);
</pre>
或是
<pre>
MATCH (me:Person {name:"My Name"}),
(movie:Movie {title:"Mystic River"})
CREATE (me)-[:REVIEWED {rating:80, summary:"tragic character movie"}]->(movie);
</pre>

##实验室：给神秘河添加影星 Kevin Bacon
先在你应该通过一个 ACTED_IN 关系来给神秘河这部电影添加一个影星 Kevin Bacon ，他出演的角色是["Sean"] (这是一个数组，但是 SET 的用法其实是一样的)。

###答案之一
<pre style="color:transparent">
MATCH (movie:Movie),(kevin:Person)
WHERE movie.title="Mystic River" AND
kevin.name="Kevin Bacon"
CREATE UNIQUE (kevin)-[:ACTED_IN {roles:["Sean"]}]->(movie)
</pre>
这个答案隐藏起来了，但是你可以像在萌娘百科里面一样通过选中来查看和复制它。
顺便问，如果我们要添加一个节点或是关系，但它可能已经存在了，该怎么办呢？
##轮到你了，试试上面学到的语句，还有视频里提到的知识点。

Gadget

小节结束
(用Cypher添加关系)-->(用Cypher添加关系·更多)
---------------------
#(图·实验室)--(更新属性)

##更新一段关系的属性

好的，那么一起试试改变 Kevin Bacon 在神秘河这电影中出演的角色，从["Sean"]改成["Sean Devine"]。
我们首先要找到两个节点之间的 ACTED_IN 关系，先用 MATCH 然后用 SET 来更新属性，就像之前创建电影的时候做的那样。

Gadget

##Solution
<pre style="color:transparent;user-select:text;-webkit-user-select:text;">
MATCH (kevin)-[r:ACTED_IN]->(mystic)
WHERE kevin.name="Kevin Bacon"
AND mystic.title="Mystic River"
SET r.roles = ["Sean Devine"]
RETURN r;
</pre>
这个答案隐藏起来了，但是你可以像在萌娘百科里面一样通过选中来查看和复制它。

小节结束
(实验室)--(编辑图：更新属性)-->(实验室)--(编辑图：添加关系)
---------------------
#(图·实验室)--(添加关系)

##给神秘河添加导演 Clint Eastwood
可以请你给神秘河添加一下导演么？导演是 Clint Eastwood ，关系名是 :DIRECTED ，不用加其他属性，对对对，好好好，就这样谢谢(老板语气)。
首先我们要取得这两个节点，就像之前在 Kevin Bacon 那里做过的一样。这应该没啥问题，去试啊别呆站着。然后我们就可以像之前一样创建一个简单地 DIRECTED 关系了。

###解法之一
<pre style="color:transparent;user-select:text;-webkit-user-select:text;">
MATCH (clint:Person),(mystic:Movie)
WHERE clint.name="Clint Eastwood" AND mystic.title="Mystic River"
CREATE (clint)-[:DIRECTED]->mystic)
RETURN clint, mystic;
</pre>
这个答案隐藏起来了，但是你可以像在萌娘百科里面一样通过选中来查看和复制它。
如果你想要保证只创建一条关系，不管你会多丧病多频繁地运行这条语句，你可以使用 MERGE 来合并。
MERGE 是一个「取得或创建」语句，它会尝试去找你指定的模式，如果找到了就返回数据，不然没找到的话就在图中创建这个结构。
<pre>
MATCH (clint:Person),(mystic:Movie)
WHERE clint.name="Clint Eastwood" AND mystic.title="Mystic River"
MERGE (clint)-[:DIRECTED]->(mystic)
RETURN clint, mystic;
</pre>

Gadget

小节结束
(实验室)--(编辑图：添加关系)-->(实验室·改)
---------------------
#(L3)--用Cypher添加关系·更多)
##匹配多个关系
现在我们尝试给所有参演和导演了同一个电影的人都创建一个 KNOWS 关系。
但是，我们得先找到他们，我们需要从两边(同时拥有参演关系和导演关系)来搜索这个图，才能找到两个需要连在一起的人。
提示：你正在搜索的关系可能是两种类型，即 [:ACTED_IN|:DIRECTED] 中间的管道符号也叫「逻辑或运算符」。
<pre>
MATCH (a)-[:ACTED_IN|:DIRECTED]->()<-[:ACTED_IN|:DIRECTED]-(b)
RETURN a,b;
</pre>
下一步是创建一个 KNOWS 关系，我们不想创建重复的关系，所以我们用 MERGE 而不是 CREATE 。
<pre>
MATCH (a)-[:ACTED_IN|:DIRECTED]->()<-[:ACTED_IN|:DIRECTED]-(b)
WHERE NOT (a)-[:KNOWS]-(b)
MERGE (a)-[:KNOWS]->(b);
</pre>

Gadget

小节结束
(用Cypher添加关系·更多)-->(实验室)
---------------------
#(L3)--(用Cypher删除节点)
之前我们在图中添加了我们自己，如果你没有这么做，用下面的魔法语句使用法术自我创造(把“My Name”换成你的名字)。
<pre>
CREATE (me:Person {name: "My Name"});
</pre>
然后咱运行下面的语句，用搜索魔法看看我们是否已经把自己封印进了图里：
<pre>
MATCH (p:Person {name:"My Name"})
RETURN p.name;
</pre>
很好，现在我们看看下面的视频，了解如何从图中删除一个节点。

VIdeo

那么，移除你自己，并斩断你身上可能存在也可能不存在的所有情缘，你需要这么做：
<pre>
MATCH (me:Person {name="My Name"})
OPTIONAL MATCH (me)-[r]-()
DELETE me,r;
</pre>
此外我们还会发现图中还有另一个需要删除的节点，运行下面的语句：
<pre>
MATCH (matrix:Movie {title:"The Matrix"})<-[r:ACTED_IN]-(actor)
RETURN actor.name, r.roles;
</pre>
这将会查找所有在黑客帝国中出演的影星并返回他们。哦，黑客帝国中可没有 Emil 这个人，所以我们得删掉这个节点。
来试试删除 Emil 吧！

Gadget

成功了么？没有？那就继续看下去，看看下一个部分。

小节结束
(用Cypher删除节点)--(用Cypher删除节点和关系)
---------------------
#(L3)--(用Cypher删除节点和关系)
###下面这个语句将会同时抹消节点和关系，即使这个草民节点在体制内没有什么关系也不会出错。
<pre>
MATCH (emil:Person)
WHERE emil.name="Emil Eifrem"
OPTIONAL MATCH (emil)-[r]-()
DELETE emil,r;
</pre>
第一个 MATCH 没啥可说的，就是查找我们想要找的节点，之后的 WHERE 语句从属于第一个 MATCH 。
第二个则是一个 OPTIONAL MATCH ，它很像SQL里面的外连接，尝试寻找匹配模式的节点，如果找不到的话就返回一个带 null 空值的行。但它总是返回至少一行结果，所以你也可以给它附加一个 WHERE 子句。
学霸可能已经开始使用这样的写法了：
<pre>
MATCH (emil:Person {name:"Emil Eifrem"})
OPTIONAL MATCH (emil)-[r]-()
DELETE emil,r;
</pre>
在下面的图中试试上面的语句：

Gadget

小节结束
(用Cypher删除节点和关系)--(超！Cypher - 朋友的朋友)
---------------------
#(L3)--(超！Cypher - 朋友的朋友)
在Cypher中，我们可以用这个菊花符号 * 来表示任意长度的路径，以便表达出攻受关系在菊花链中的复杂性。
<pre>
MATCH (node1)-[*]-(node2)
</pre>
<img class="unselectable js-image" unselectable="on" src="//static.versal.com/restapi/assets/4fd950de-20df-445f-a183-e4f7d320bb23" style="left: -3px; top: -1px; width: 728px; max-width: 728px; height: 409px; max-height: 409px;">
如果你想要遍历四层深的关系，可以试试 (a)-[*1..4]->(b)
如果你想要遍历任意深的关系，可以这样 (a)-[*]->(b)
如果你想遍历指定深度的关系，可以用如 (a)-[*3]->(b) 表示找到所有精确地就是三次联系的关系。
当然了，你可以限制关系的类型，比如想找朋友的朋友就用 [:FRIENDS*2] ，以表明你只找朋友关系而不是炮友关系。

小节结束
(超！Cypher - 朋友的朋友)--(图·实验室)
---------------------
#(图·实验室)--(朋友的朋友)

1.试着返回 Keanu Reeves 所有的朋友的朋友（之间的关系是 KNOWS ）
2.你可以如何优化一下，只取得并不是他直接朋友的人呢？

Gadget

##第一题的解法之一
这个答案隐藏起来了，但是你可以像在萌娘百科里面一样通过选中来查看和复制它。
<pre style="color:transparent;user-select:text;-webkit-user-select:text;">
MATCH (keanu:Person)-[:KNOWS*2]->(fof)
WHERE keanu.name = "Keanu Reeves"
RETURN DISTINCT fof.name;
</pre>
##第二题的解法之一
<pre style="color:transparent;user-select:text;-webkit-user-select:text;">
MATCH (keanu:Person)-[:KNOWS*2]->(fof)
WHERE keanu.name = "Keanu Reeves"
AND NOT((keanu)-[:KNOWS]-(fof))
RETURN DISTINCT fof.name;
</pre>

小节结束
(图·实验室)--(图·实验室·改)
---------------------
#(图·实验室·改)--(最短路径与Bacon Number)

##怎么算出 Keanu Reeves 的<a href="http://www.douban.com/note/236880817/" target="_blank">Bacon Number</a>呢？
在 Neo4j 里有一个函数叫做 shortestPath() 可以帮到你。它返回的是路径，就像 MATCH 语句里面你的一个模式那样，而且是最短的一条。你可以这样使用它：
<pre>
MATCH p=shortestPath( (node1)-[*]-(node2) )
RETURN length(p), nodes(p)
</pre>
在我们想解决的问题里，我们需要同时找到 Keanu Reeves 和 Kevin Bacon 并用我们的 KNOWS 关系来找到他们两人之间的最短距离。
<pre>
MATCH p=shortestPath( (keanu:Person)-[:KNOWS*]-(kevin:Person) )
WHERE keanu.name="Keanu Reeves" and kevin.name = "Kevin Bacon"
RETURN length(p)
</pre>
或是用另一种写法，我们先找到两端的节点，再找最短路径：
<pre>
MATCH (keanu:Person {name:"Keanu Reeves"}),
(kevin:Person {name:"Kevin Bacon"})
MATCH p=shortestPath((keanu)-[:KNOWS*]->(kevin))
RETURN length(p);
</pre>
##下一个任务是只返回把 Keanu 和 Kevin 连接起来的人
我们将要使用的是一个集合函数，在<a href="http://docs.neo4j.org/chunked/stable/cypher-query-lang.html" target="_blank">Neo4j 手册</a>和<a href="http://docs.neo4j.org/refcard/2.0/" target="_blank">Cypher作弊卡</a>里有着事无巨细悉究本末的参考资料，稍后去看看无妨。
一条路径就是一系列连着关系的散布节点，我们可以用 nodes(path) 来访问这些节点的集合，而我们要用的聚合函数是 extract 提取，可以将一个表达式对集合中的每个元素都运行一次，并返回结果值而不是集合中的元素，语法是 extract( x in collection | expression )
例如：
- extract ( x in [1,2,3] | x*x) 返回一列的平方值
- extract ( n in nodes(path) | n.name ) 返回一列的名字
- [ x in collection | expression ] 是它的简略写法
来试试看：
<pre>
MATCH (keanu:Person {name:"Keanu Reeves"}),
(kevin:Person {name:"Kevin Bacon"})
MATCH p=shortestPath((keanu)-[:KNOWS*]->(kevin))
RETURN [ n in nodes(p) | n.name ];
</pre>
展开表格就可以看到所有集合中的元素了，也就是路径中的所有元素了。
如果你想要跳过第一个人或是最后一个人的名字，你可以像数组那样使用集合下标 [2..5] 或在我们的例子里是 [1..-1] 表示从第二个元素到倒数第二个元素。
<pre>
MATCH (keanu:Person {name:"Keanu Reeves"}),
(kevin:Person {name:"Kevin Bacon"})
MATCH p=shortestPath((keanu)-[:KNOWS*]->(kevin))
RETURN [ n in nodes(p)[1..-1] | n.name ];
</pre>
#返回把 Keanu 和 Kevin 连接起来的人

Gadget

---------------------
#(L3)--(复习第三课)

Video

那么我们来看些例子：
创建节点：
<pre>
CREATE (m:Movie {title:"Mystic River", released:1993});
</pre>
创建关系：
<pre>
MATCH (movie:Movie {title:"Mystic River"),
(kevin:Person {name:"Kevin Bacon"})
MERGE (kevin)-[r:ACTED_IN]->(movie)
ON CREATE SET r.roles=["Sean"]
</pre>
为节点或关系添加属性：
<pre>
MATCH (movie:Movie {title:"Mystic River"})
SET movie.tagline = "We bury our sins here, Dave. We wash them clean."
RETURN movie;
</pre>
更新节点或关系的属性：
<pre>
MATCH (movie:Movie {title:"Mystic River"})
SET movie.released = 2003
RETURN movie;
</pre>
删除节点：
<pre>
MATCH (emil:Person {name:"Emil Eifrem"})
OPTIONAL MATCH (emil)-[r]-()
DELETE emil, r;
</pre>
注意我们也同时做了删除关系的动作。你无法在删光一个节点带的关系之前删除它，尽管你可以在一个语句里同时做这两件事。
##复习第三课学的语句，在Cypher查询窗口里练练

Gadget

第三课结束
---------------------
#第四课-Neo4j实战

Video

我们发现同志们在真正起手使用Neo4j的时候，遇到的最大困难是数据建模，所以在这一课里我们将看看如何在Neo4j里高效地建模，并观察一些其他同志使用Neo4j的例子，进而向着Neo4j在贵机构里的落户卖出重要的下一步。

开始第四课(L4)
---------------------
(L4)--(Neo4j中的数据建模)

##从一个白板开始
Neo4j与其他数据库技术比起来最神的地方在于你可以用一个白板来尝试建模。
<img src="//static.versal.com/restapi/assets/60028bb1-c7e5-477b-bd58-c0345975ce23">
像上面那样的一个草图可以很轻易地转化成数据库里的一个图。
<img src="//static.versal.com/restapi/assets/34c63690-d462-4834-b617-1f7d57af16ea">
同样，我们需要一步一步搭建起我们的模型，你没必要一下子把所有可能的数据都倒进你的模型里，而应该从一个相当简单的模型开始。
<img src="//static.versal.com/restapi/assets/99b0de84-5741-4f46-a045-d8aade86f12e" >
接着再随着时间改良模型，加入更多信息。
<img src="//static.versal.com/restapi/assets/53439f4e-e9d8-49b1-b9a3-68abc3d30af6">

##为重要的实体提供索引
对于模型中比较重要经常查到的实体，比如在我们的例子里是电影和人，我们建议给他们加上自动索引。
记住语法 CREATE INDEX ON :Label(property) 在我们的模型中我们使用的是：
-CREATE INDEX ON :Movie(title)
-CREATE INDEX ON :Person(name)

##匹配模式的语句
然而，不像用SQL的年代那样，你不会只想要找到所有movie列的值是 云图Cloud Atlas 的人，然后返回名字，以此查找所有在这个电影里出演的人。
你会想的是，确认的确存在 云图Cloud Atlas 这个节点之后，它的身上会有从出演者来的 :ACTED_IN 关系。
然后你就可以在查询语句里写一个模式来表示现实生活中的「某人参演了电影」： (m:Movie)<-[:ACTED_IN]-(actor) 并在图里找出它。
<pre>
MATCH (m:Movie)<-[:ACTED_IN]-(actor)
WHERE m.title = "Cloud Atlas"
RETURN actor;
</pre>
你可以用上别的写法：
<pre>
MATCH (m:Movie {title: 'Cloud Atlas'})<-[:ACTED_IN]-(actor)
RETURN actor;
</pre>
同时通过上面提到的索引，Cypher将会机智地发现你是通过 title 来查找一个 :Movie ，而她知道已经存在标签关于标题的索引，她会使用索引来快速查找到电影。
没有索引的时候查询语句依然可以工作，但是她将会扫描所有的 Movie 节点并用你提供的值来比较它们的 title 属性。

小节结束
(Neo4j中的数据建模)--(Neo4j案例学习：Accenture)
---------------------
#(L4)--(Neo4j案例学习：Accenture)

Video

##背景
Accenture公司接受了世界上最大的物流公司之一的外包，帮助它替换掉呆笨沉重的原有系统，他们写出了一个新的包裹路由系统来提供每天 5M 个包裹的实时路由。
##商业问题
系统需要提供 24×7的高可用以及峰值每秒2500+的访问，它需要与一个繁杂的软件栈协作并提供可预知的性能和线性可扩展性。它同时还需要处理每天对潜在物流网络的更新。
##解决方案和益处
Neo4j是这个领域完美的搭档，因为物流网络是很基础的图论对象。Neo4j的聚类可以保证高可用和高性能，与关系性数据库相比，她的查询语句写起来维护起来也很方便，而且灵活的数据模型似的她很容易表示现实生活中的数据变量。

小节结束
(Neo4j案例学习：Accenture)--(Neo4j案例学习：Glassdoor)
---------------------
#(L4)--(Neo4j案例学习：Glassdoor)

Video

##背景
Glassdoor是一家提供信息分析的公司，帮助求职者更高效地通过他们的社交网络延伸出去找到工作。
##商业问题
他们发现有许多工作机会都是通过熟人联系得到的，因此这个系统的目标是让人们可以在导入Facebook关系网之后，得到关于更有趣的工作机会的信息。
##解决方案和益处
Glassdoor创造了一个 first to market 解决方案帮助人们通过Facebook好友找到工作，每个独立的Facebook好友地图都被实时导入Neo4j中，所以现在Glassdoor已经拥有了超过一半的Facebook好友地图。Neo4j集群可以无缝垂直扩展，每一个新的实例上线后都可以似的图的大小和负载无缝提升。

小节结束
(Neo4j案例学习：Glassdoor)--(Neo4j案例学习：SFR)
---------------------
#(L4)--(Neo4j案例学习：SFR)
Video
##背景
SFR是法国第二大的通信技术公司。
##商业问题
从前，每一次对基础设施的维护都需要提前一整周来计划，因为需要花很多时间估计停工对每个组件的潜在网络影响。他们需要能够进行快速的自动后果分析以保证维护过程中网络还有弹性可言，并得发觉网络中的薄弱点。
##解决方案和益处
Neo4j在投入使用后被作为展示整个网络信息的唯一资讯源，因为即使从30+个不同系统中导入了数据后，她的模型依然能够高效地 1：1 还原整个现实世界中的网络。

小节结束
(Neo4j案例学习：SFR)--(社区资源)
---------------------
#(L4)--(下一步…以及社区资源)

Video

##现场教学
如果你觉得在线版本的教程蛮不错的，翻译得也很逗，但是你就是想听听语速超快的英文自虐，虽然顺带着你可以学到更多Neo4j的知识，你可以试试我们提供的现场<a href="http://www.neo4j.org/participate/events/trainings?ref=online_training" target="_blank">手把手教学</a>。它在全球都有供应，教官经验丰富久经沙场。
##Neo4j社区
世上没有不散的筵席，不管第二天的饱，就像没有什么教程能够回答你所有的技术问题，但幸运的是你可以加入人民公社——Neo4j有一个很活跃的社区。
如果你想学到更多关于Neo4j的知识，从<a href="http://neo4j.org?ref=online_training" target="_blank">Neo4j.org</a>开始，这是提供了很多深入学习Neo4j的选项的社区网站。
如果你遇到了比较神奇的问题，也可以上<a href="http://stackoverflow.com/questions/tagged/neo4j" target="_blank">Stackoverflow</a>看看大神的意见，这里回复还是蛮快的。或是到<a href="http://neo4j.org/forums?ref=online_training" target="_blank">Neo4j Google Group</a>来讨论图建模问题、Neo4j生态圈还有产品功能什么的。
如果你想看看Neo4j背后的更多公司及企业级功能、我们可以提供的支持包，你应该到<a href="http://neotechnology.com" target="_blank">NeoTechnology.com</a>看看。
翻墙Fo我们的Twitter <a href="http://twitter.com/neo4j" target="_blank">@Neo4j</a>，然后肉身翻墙看看<a href="http://www.neo4j.org/participate/events/meetups?ref=online_training" target="_blank">Neo4j meetups</a>，与其他高富帅白富美图工程师面对面。
-<a href="http://www.neo4j.org/download?ref=online_training" target="_blank">下载 Neo4j</a>并开发你的第一个Neo4j项目
-在<a href="http://stackoverflow.com/questions/tagged/neo4j" target="_blank">Stack Overflow</a>上问问题
-在<a href="http://neo4j.org/forums?ref=online_training" target="_blank">Neo4j's Google Group</a>上讨论新点子
-瞅瞅<a href="http://docs.neo4j.org/" target="_blank">Neo4j Documentation</a>
-使用<a href="http://docs.neo4j.org/refcard/2.0" target="_blank">Neo4j Cypher Reference Card</a>

第四课结束
---------------------
#课程调查




